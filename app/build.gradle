apply plugin: 'com.android.application'

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')
    compile 'commons-io:commons-io:2.0.1'
    compile 'com.googlecode.json-simple:json-simple:1.1'
    // TODO: This did not work
    //compile 'com.googlecode.json-simple:json-simple:1.1.1'
    // TODO: start using Guava
    //compile 'com.google.guava:guava:11.0.2'
    compile 'com.koushikdutta.async:androidasync:2.+'
}

android {
    // We support API level 8, but use isChangingConfigurations() when API 11+
    // is available.
    compileSdkVersion 21
    buildToolsVersion "21.1.1"


    // API level 3: some default button was not found, but we didn't explore it further.
    // API level 7: MediaRecorder.AudioSource.VOICE_RECOGNITION
    // API level 8: android.speech.SpeechRecognizer and android.speech.RecognitionService
    defaultConfig {
        applicationId 'ee.ioc.phon.android.speak'
        minSdkVersion 8
        // TODO: make this the highest possible one we do not rely on the old-style menus anymore
        targetSdkVersion 8
        versionCode 848
        versionName '0.8.48'
    }


    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    signingConfigs {
        release {
            storeFile file(System.getenv("KEYSTORE"))
            keyAlias System.getenv("KEY_ALIAS")
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFile getDefaultProguardFile('proguard-android.txt')
            proguardFile 'proguard.cfg'
            signingConfig signingConfigs.release
        }
    }
}


if (project.hasProperty('storePassword') &&
    project.hasProperty('keyPassword')) {
    android.signingConfigs.release.storePassword = storePassword
    android.signingConfigs.release.keyPassword = keyPassword
} else {
    android.buildTypes.release.signingConfig = null
}
